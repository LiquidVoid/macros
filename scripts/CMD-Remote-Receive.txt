if( MODULEHTTP == true );

    do;
        &reply = httpget("%@&remote_http%","source=%PLAYER%&target=%PLAYER%&action=get",#status);

        if( ( #status == 200 ) && ( &reply != "" ) );
            &msg = match("%&reply%","^%PLAYER%:{2}echo:{2}(.+)$","1");
            logto("ScriptLog","%&msg%");
            wait("250ms");
            @&queue[6] = %&msg%;
            #tries = 0;

        elseif( ( #status == 509 ) || ( #status == -2 ) );

            if( #tries <= 1200 );
                logto("ScriptLog","%#status%: Bandwidth Limit Exceeded. %@&colours_text_default%[%@&colours_variable_global%%@&remote_http%%@&colours_text_default%]");

            endif;

            wait("600");
            #tries = %#tries% + 600;

        elseif( #status == 408 );

            if( #tries <= 40 );
                logto("ScriptLog","%#status%: Request Timeout. %@&colours_text_default%[%@&colours_variable_global%%@&remote_http%%@&colours_text_default%]");

            endif;

            wait("20");
            #tries = %#tries% + 20;

        elseif( #status == -1 );

            if( #tries <= 40 );
                logto("ScriptLog","ERROR: No Reason Given. %@&colours_text_default%[%@&colours_variable_global%%@&remote_http%%@&colours_text_default%]");

            endif;

            wait("20");
            #tries = %#tries% + 20;


        elseif( #status != 200 );

            if( #tries <= 10 );
                logto("ScriptLog","%#status%: ERROR %@&colours_text_default%[%@&colours_variable_global%%@&remote_http%%@&colours_text_default%]");

            endif;

            wait("5");
            #tries = %#tries% + 5;

        else;

            if( #tries <= 2 );
                logto("Debug","%@&remote_http%/%#status%: %&reply%");

            endif;

            wait("1");
            #tries = %#tries% + 1;

        endif;

    while( #tries <= 1200 );

else;
    logto("ErrorLog","Could not run %@&colours_text_highlight%CMD-Remote-Receive");

    if( MODULEHTTP == false );
        wait("2");
        logto("ErrorLog","Missing: %@&colours_variable_global%MODULEHTTP");

    endif;

endif;
